      SUBROUTINE SVDVAR(VMATRX,MCOEFF,NPHYS,WVECTR,COVARS,NCOVMX)

C     *************************************************************************
C
C     SVDVAR
C     ======
C
C     AUTHOR
C     ------
C     R.S.CANT  --  CAMBRIDGE UNIVERSITY ENGINEERING DEPARTMENT
C
C     CHANGE RECORD
C     -------------
C     29-OCT-2012:  CREATED
C      
C     DESCRIPTION
C     -----------
C     GENERAL LINEAR LEAST-SQUARES FIT
C       FINDS VARIANCES AND COVARIANCES OF THE FIT PARAMETERS
C     TAKES THE MATRIX V AND VECTOR W FROM THE SVD OF THE DESIGN MATRIX
C     RETURNS THE COVARIANCE MATRIX COVARS
C     NOTE: MUST HAVE NPHYS >= MCOEFF
C                     NCOVMX >= NPOINT
C
C     BASED ON NUMERICAL RECIPES SUBROUTINE SVDVAR
C
C     *************************************************************************

C     PARAMETERS
C     ==========
      DOUBLE PRECISION ZERO,ONE
      PARAMETER(ZERO = 0.0D0, ONE = 1.0D0)

      INTEGER MCOFMX
      PARAMETER(MCOFMX = 10)


C     ARGUMENTS
C     =========
      DOUBLE PRECISION VMATRX(NPHYS,NPHYS)
      DOUBLE PRECISION WVECTR(NPHYS)
      DOUBLE PRECISION COVARS(NCOVMX,NCOVMX)
      INTEGER MCOEFF,NPHYS,NCOVMX


C     LOCAL DATA
C     ==========
      DOUBLE PRECISION OVWSQR(MCOFMX)
      DOUBLE PRECISION VARSUM
      INTEGER IC,JC,KC


C     BEGIN
C     =====
      DO IC = 1,MCOEFF
        OVWSQR(IC) = ZERO
        IF(WVECTR(IC).NE.ZERO)OVWSQR(IC) = ONE/(WVECTR(IC)*WVECTR(IC))
      ENDDO

C     SUM CONTRIBUTIONS TO COVARIANCE MATRIX
      DO IC = 1,MCOEFF
        DO JC = 1,IC
          VARSUM = ZERO
          DO KC = 1,MCOEFF
            VARSUM = VARSUM + VMATRX(IC,KC)*VMATRX(JC,KC)*OVWSQR(KC)
          ENDDO
          COVARS(IC,JC) = VARSUM
          COVARS(JC,IC) = VARSUM
        ENDDO
      ENDDO


      RETURN
      END
